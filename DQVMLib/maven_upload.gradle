apply plugin: 'maven'
apply plugin: 'signing'
configurations {
    deployerJars
}
repositories {
    mavenCentral()
}
// 判断版本是Release or Snapshots
def isReleaseBuild() {
    return !VERSION_NAME.contains("T")
}
// 获取仓库url
def getRepositoryUrl() {
    return isReleaseBuild() ? MAVEN_RELEASE_URL : MAVEN_SNAPSHOT_URL
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                deployment -> signing.signPom(deployment)
            }
            pom.project {
                name MAVEN_TAG
                url MAVEN_URL
                version VERSION_NAME
                groupId MAVEN_GROUP_ID
                artifactId MAVEN_ARTIFACT_ID
                packaging MAVEN_PACKAGING
                description MAVEN_DESCRIPTION
                scm {
                    url MAVEN_URL
                    tag MAVEN_TAG
                    connection SCM
                    developerConnection SCM
                }
                developers {
                    developer {
                        name DEVELOPER_NAME
                        email DEVELOPER_EMAIL
                    }
                }
                licenses {
                    license {
                        name LICENSES_NAME
                        url LICENSES_URL
                    }
                }
            }
            repository(url: getRepositoryUrl()) {
                authentication(userName: MAVEN_NAME, password: MAVEN_PASSWORD)
            }
        }
    }
}

signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}
